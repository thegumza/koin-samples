apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

apply from: '../../gradle/versions-examples.gradle'

android {
    compileSdkVersion android_target_version
    buildToolsVersion android_build_tools_version
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        applicationId "koin.sampleapp.mvvm"
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'

        // used by Room, to test migrations
        androidTest.assets.srcDirs +=
                files("$projectDir/schemas".toString())
    }
}

dependencies {

    // Android Support
    implementation "androidx.appcompat:appcompat:$support_lib_version"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "com.google.android.material:material:1.0.0"

    // Android Test
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-inline:$mockito_version"
    androidTestImplementation 'androidx.test:runner:1.1.2-alpha02'
    androidTestUtil 'androidx.test:orchestrator:1.1.2-alpha02'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Anko
    implementation "org.jetbrains.anko:anko-commons:$anko_version"

    // Koin
    implementation "org.koin:koin-android-viewmodel:$koin_version"
    testImplementation "org.koin:koin-test:$koin_version"
    androidTestImplementation "org.koin:koin-test:$koin_version"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$androidx_arch_version"
    testImplementation "androidx.arch.core:core-testing:2.0.0"

    // Room
    implementation "androidx.room:room-runtime:2.1.0-alpha05"
    implementation "androidx.room:room-rxjava2:2.1.0-alpha05"
    kapt "androidx.room:room-compiler:2.1.0-alpha05"
    testImplementation "androidx.room:room-testing:2.1.0-alpha05"

    // UI
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation 'com.joanzapata.iconify:android-iconify-weathericons:2.2.2'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.5'

    // Networking
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    // Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
}

